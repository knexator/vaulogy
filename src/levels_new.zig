const presenter = @import("presenter.zig");
const funk = @import("kommon").funktional;
const BuiltinLevel = presenter.BuiltinLevel;
const Sample = presenter.Sample;
const core = @import("main.zig");
const Sexpr = core.Sexpr;
const std = @import("std");
const assert = std.debug.assert;

const SexprPool = std.heap.MemoryPool(Sexpr);
const kommon = @import("kommon");

fn safeAt(arr: []const *const Sexpr, index: usize) ?*const Sexpr {
    return kommon.safeAt(*const Sexpr, arr, index);
}

pub const Level = struct {
    fnk_name: *const Sexpr,
    generate_sample: *const fn (k: usize, pool: *SexprPool, arena: std.mem.Allocator) core.OoM!?Sample,

    pub fn samplesIterator(level: Level) SamplesIterator {
        return .{ .k = 0, .level = level };
    }

    pub const SamplesIterator = struct {
        k: usize,
        level: Level,

        pub fn next(self: *SamplesIterator, pool: *SexprPool, arena: std.mem.Allocator) !?Sample {
            const result = try self.level.generate_sample(self.k, pool, arena);
            if (result) |r| {
                self.k += 1;
                return r;
            } else return null;
        }
    };
};

const Solutions = struct {
    fn isVowel(in: *const Sexpr) ?*const Sexpr {
        return Sexpr.fromBool(Helpers.isVowel(in));
    }
};

const Helpers = struct {
    fn isVowel(in: *const Sexpr) bool {
        return (in.equals(Vals.lowercase[0]) or in.equals(Vals.lowercase[4]) or
            in.equals(Vals.uppercase[0]) or in.equals(Vals.uppercase[4]));
    }

    fn isB(in: *const Sexpr) bool {
        return in.equals(Vals.uppercase[1]) or in.equals(Vals.lowercase[1]);
    }

    fn hasSomeB(in: *const Sexpr) bool {
        if (in.isPair()) {
            return hasSomeB(in.pair.left) or hasSomeB(in.pair.right);
        } else return isB(in);
    }
};

pub const levels: []const Level = &.{
    .{
        .fnk_name = &Sexpr.doLit("uppercase"),
        .generate_sample = struct {
            fn generate_sample(k: usize, _: *SexprPool, _: std.mem.Allocator) core.OoM!?Sample {
                if (k < Vals.lowercase.len) {
                    return .{ .input = Vals.lowercase[k], .output = Vals.uppercase[k] };
                } else return null;
            }
        }.generate_sample,
    },
    .{
        .fnk_name = &Sexpr.doLit("lowercase"),
        .generate_sample = struct {
            fn generate_sample(k: usize, _: *SexprPool, _: std.mem.Allocator) core.OoM!?Sample {
                if (k < Vals.lowercase.len) {
                    return .{ .input = Vals.uppercase[k], .output = Vals.lowercase[k] };
                } else return null;
            }
        }.generate_sample,
    },
    .{
        .fnk_name = &Sexpr.doLit("isVowel"),
        .generate_sample = struct {
            fn generate_sample(k: usize, _: *SexprPool, _: std.mem.Allocator) core.OoM!?Sample {
                const both = Vals.lowercase ++ Vals.uppercase;
                if (k < both.len) {
                    return .{
                        .input = both[k],
                        .output = Solutions.isVowel(both[k]),
                    };
                } else return null;
            }
        }.generate_sample,
    },
    .{
        .fnk_name = &Sexpr.doLit("swap"),
        .generate_sample = struct {
            fn generate_sample(k: usize, pool: *SexprPool, _: std.mem.Allocator) core.OoM!?Sample {
                if (k < 100) {
                    var random_instance: std.Random.DefaultPrng = .init(@intCast(k));
                    const random = random_instance.random();
                    const left = try randomSexpr(pool, &(Vals.lowercase ++ Vals.uppercase), random, 4);
                    const right = try randomSexpr(pool, &(Vals.lowercase ++ Vals.uppercase), random, 4);
                    return .{
                        .input = try store(pool, Sexpr.doPair(left, right)),
                        .output = try store(pool, Sexpr.doPair(right, left)),
                    };
                } else return null;
            }
        }.generate_sample,
    },
    .{
        .fnk_name = &Sexpr.doLit("firstAsUppercase"),
        .generate_sample = struct {
            fn generate_sample(k: usize, pool: *SexprPool, _: std.mem.Allocator) core.OoM!?Sample {
                const k1 = @mod(k, Vals.lowercase.len);
                const k2 = @divFloor(k, Vals.lowercase.len);
                if (k2 < Vals.lowercase.len) {
                    return .{
                        .input = try store(pool, Sexpr.doPair(Vals.lowercase[k1], Vals.lowercase[k2])),
                        .output = Vals.uppercase[k1],
                    };
                } else return null;
            }
        }.generate_sample,
    },
    .{
        .fnk_name = &Sexpr.doLit("startWithB"),
        .generate_sample = struct {
            fn generate_sample(k: usize, pool: *SexprPool, _: std.mem.Allocator) core.OoM!?Sample {
                const both = Vals.lowercase ++ Vals.uppercase;
                const k1 = @mod(k, both.len);
                const k2 = @divFloor(k, both.len);
                if (k2 < both.len) {
                    return .{
                        .input = try store(pool, Sexpr.doPair(both[k1], both[k2])),
                        .output = Sexpr.fromBool(Helpers.isB(both[k1])),
                    };
                } else return null;
            }
        }.generate_sample,
    },
    .{
        .fnk_name = &Sexpr.doLit("uppercaseIfVowel"),
        .generate_sample = struct {
            fn generate_sample(k: usize, _: *SexprPool, _: std.mem.Allocator) core.OoM!?Sample {
                if (k < Vals.lowercase.len) {
                    const in = Vals.lowercase[k];
                    return .{
                        .input = in,
                        .output = if (Helpers.isVowel(in))
                            Vals.uppercase[k]
                        else
                            in,
                    };
                } else return null;
            }
        }.generate_sample,
    },
    .{
        .fnk_name = &Sexpr.doLit("firstToUppercase"),
        .generate_sample = struct {
            fn generate_sample(k: usize, pool: *SexprPool, _: std.mem.Allocator) core.OoM!?Sample {
                const k1 = @mod(k, Vals.lowercase.len);
                const k2 = @divFloor(k, Vals.lowercase.len);
                if (k2 < Vals.lowercase.len) {
                    return .{
                        .input = try store(pool, Sexpr.doPair(Vals.lowercase[k1], Vals.lowercase[k2])),
                        .output = try store(pool, Sexpr.doPair(Vals.uppercase[k1], Vals.lowercase[k2])),
                    };
                } else return null;
            }
        }.generate_sample,
    },
    .{
        .fnk_name = &Sexpr.doLit("pairToUppercase"),
        .generate_sample = struct {
            fn generate_sample(k: usize, pool: *SexprPool, _: std.mem.Allocator) core.OoM!?Sample {
                const k1 = @mod(k, Vals.lowercase.len);
                const k2 = @divFloor(k, Vals.lowercase.len);
                if (k2 < Vals.lowercase.len) {
                    return .{
                        .input = try store(pool, Sexpr.doPair(Vals.lowercase[k1], Vals.lowercase[k2])),
                        .output = try store(pool, Sexpr.doPair(Vals.uppercase[k1], Vals.uppercase[k2])),
                    };
                } else return null;
            }
        }.generate_sample,
    },
    .{
        .fnk_name = &Sexpr.doLit("changeCase"),
        .generate_sample = struct {
            fn generate_sample(k: usize, _: *SexprPool, _: std.mem.Allocator) core.OoM!?Sample {
                if (k < Vals.lowercase.len) {
                    return .{
                        .input = Vals.lowercase[k],
                        .output = Vals.uppercase[k],
                    };
                } else if (k < Vals.lowercase.len * 2) {
                    return .{
                        .input = Vals.uppercase[k - Vals.lowercase.len],
                        .output = Vals.lowercase[k - Vals.lowercase.len],
                    };
                } else return null;
            }
        }.generate_sample,
    },
    .{
        .fnk_name = &Sexpr.doLit("letterToBothCases"),
        .generate_sample = struct {
            fn generate_sample(k: usize, pool: *SexprPool, _: std.mem.Allocator) core.OoM!?Sample {
                if (k < Vals.lowercase.len) {
                    return .{
                        .input = Vals.lowercase[k],
                        .output = try store(pool, Sexpr.doPair(Vals.lowercase[k], Vals.uppercase[k])),
                    };
                } else if (k < Vals.lowercase.len * 2) {
                    const k2 = k - Vals.lowercase.len;
                    return .{
                        .input = Vals.uppercase[k2],
                        .output = try store(pool, Sexpr.doPair(Vals.lowercase[k2], Vals.uppercase[k2])),
                    };
                } else return null;
            }
        }.generate_sample,
    },
    .{
        .fnk_name = &Sexpr.doLit("hasSomeB"),
        .generate_sample = struct {
            fn generate_sample(k: usize, pool: *SexprPool, _: std.mem.Allocator) core.OoM!?Sample {
                if (k < 100) {
                    var random_instance: std.Random.DefaultPrng = .init(@intCast(k));
                    const random = random_instance.random();
                    const input = try randomSexpr(pool, &Vals.lowercase, random, 5);
                    return .{
                        .input = input,
                        .output = Sexpr.fromBool(Helpers.hasSomeB(input)),
                    };
                } else return null;
            }
        }.generate_sample,
    },
    .{
        .fnk_name = &Sexpr.doLit("second"),
        .generate_sample = struct {
            fn generate_sample(k: usize, pool: *SexprPool, _: std.mem.Allocator) core.OoM!?Sample {
                if (k < 100) {
                    var random_instance: std.Random.DefaultPrng = .init(@intCast(k));
                    const random = random_instance.random();
                    const first = randomChoice(&Vals.lowercase, random);
                    const second = randomChoice(&Vals.lowercase, random);
                    const rest = try randomList(pool, &Vals.lowercase, random, random.intRangeAtMost(usize, 0, 7));
                    return .{
                        .input = try toListWithSentinel(pool, &.{ first, second }, rest),
                        .output = second,
                    };
                } else return null;
            }
        }.generate_sample,
    },
    .{
        .fnk_name = &Sexpr.doLit("listHasSomeB"),
        .generate_sample = struct {
            fn generate_sample(k: usize, pool: *SexprPool, _: std.mem.Allocator) core.OoM!?Sample {
                if (k == 0) {
                    return .{
                        .input = Sexpr.builtin.nil,
                        .output = Sexpr.builtin.false,
                    };
                } else if (k < 100) {
                    var random_instance: std.Random.DefaultPrng = .init(@intCast(k));
                    const random = random_instance.random();
                    var remaining_len = 1 + random.uintLessThan(usize, @min(k, 9));
                    // long samples
                    if (k > 90) remaining_len += 50;
                    var input = Sexpr.builtin.nil;
                    var has_b = false;
                    while (remaining_len > 0) : (remaining_len -= 1) {
                        const v = if (random.float(f32) < 0.2)
                            Vals.lowercase[1]
                        else
                            try randomSexpr(pool, &Vals.lowercase, random, 3);
                        has_b = has_b or Helpers.isB(v);
                        input = try store(pool, Sexpr.doPair(v, input));
                    }
                    return .{
                        .input = input,
                        .output = Sexpr.fromBool(has_b),
                    };
                } else return null;
            }
        }.generate_sample,
    },
    .{
        .fnk_name = &Sexpr.doLit("uppercaseEachElement"),
        .generate_sample = struct {
            fn generate_sample(k: usize, pool: *SexprPool, _: std.mem.Allocator) core.OoM!?Sample {
                if (k == 0) {
                    return .{
                        .input = Sexpr.builtin.nil,
                        .output = Sexpr.builtin.nil,
                    };
                } else if (k < 100) {
                    var random_instance: std.Random.DefaultPrng = .init(@intCast(k));
                    const random = random_instance.random();
                    var remaining_len = 1 + random.uintLessThan(usize, @min(k, 9));
                    // long samples
                    if (k > 90) remaining_len += 50;
                    var input = Sexpr.builtin.nil;
                    var output = Sexpr.builtin.nil;
                    while (remaining_len > 0) : (remaining_len -= 1) {
                        const i = random.uintLessThan(usize, Vals.lowercase.len);
                        input = try store(pool, Sexpr.doPair(Vals.lowercase[i], input));
                        output = try store(pool, Sexpr.doPair(Vals.uppercase[i], output));
                    }
                    return .{
                        .input = input,
                        .output = output,
                    };
                } else return null;
            }
        }.generate_sample,
    },
    .{
        .fnk_name = &Sexpr.doLit("hasAnyVowel"),
        .generate_sample = struct {
            fn generate_sample(k: usize, pool: *SexprPool, _: std.mem.Allocator) core.OoM!?Sample {
                if (k == 0) {
                    return .{
                        .input = Sexpr.builtin.nil,
                        .output = Sexpr.builtin.false,
                    };
                } else if (k < 100) {
                    var random_instance: std.Random.DefaultPrng = .init(@intCast(k));
                    const random = random_instance.random();
                    var remaining_len = 1 + random.uintLessThan(usize, @min(k, 9));
                    // long samples
                    if (k > 90) remaining_len += 50;
                    var input = Sexpr.builtin.nil;
                    var output = false;
                    while (remaining_len > 0) : (remaining_len -= 1) {
                        const i = random.uintLessThan(usize, Vals.lowercase.len);
                        input = try store(pool, Sexpr.doPair(Vals.lowercase[i], input));
                        output = output or Helpers.isVowel(Vals.lowercase[i]);
                    }
                    return .{
                        .input = input,
                        .output = Sexpr.fromBool(output),
                    };
                } else return null;
            }
        }.generate_sample,
    },
    .{
        .fnk_name = &Sexpr.doLit("reverse"),
        .generate_sample = struct {
            fn generate_sample(k: usize, pool: *SexprPool, arena: std.mem.Allocator) core.OoM!?Sample {
                if (k == 0) {
                    return .{
                        .input = Sexpr.builtin.nil,
                        .output = Sexpr.builtin.nil,
                    };
                } else if (k < 100) {
                    var random_instance: std.Random.DefaultPrng = .init(@intCast(k));
                    const random = random_instance.random();
                    var remaining_len = 1 + random.uintLessThan(usize, @min(k, 9));
                    // long samples
                    if (k > 90) remaining_len += 50;
                    var input = Sexpr.builtin.nil;
                    var output: std.ArrayListUnmanaged(*const Sexpr) = try .initCapacity(arena, remaining_len);
                    while (remaining_len > 0) : (remaining_len -= 1) {
                        const i = random.uintLessThan(usize, Vals.lowercase.len);
                        input = try store(pool, Sexpr.doPair(Vals.lowercase[i], input));
                        output.appendAssumeCapacity(Vals.lowercase[i]);
                    }
                    return .{
                        .input = input,
                        .output = try toList(pool, output.items),
                    };
                } else return null;
            }
        }.generate_sample,
    },
    .{
        .fnk_name = &Sexpr.doLit("mostCommonBoolean"),
        .generate_sample = struct {
            fn generate_sample(k: usize, pool: *SexprPool, arena: std.mem.Allocator) core.OoM!?Sample {
                const t = Sexpr.builtin.true;
                const f = Sexpr.builtin.false;
                const premade_samples: []const struct { input: []const *const Sexpr, output: *const Sexpr } = &.{
                    .{
                        .input = &.{ t, f, t },
                        .output = t,
                    },
                    .{
                        .input = &.{ t, f, f, t, f },
                        .output = f,
                    },
                    .{
                        .input = &.{ t, t, t, f, f, f, f },
                        .output = f,
                    },
                    .{
                        .input = &.{ t, f, t, f, t, f, t },
                        .output = t,
                    },
                };
                if (k < premade_samples.len) {
                    return .{
                        .input = try toList(pool, premade_samples[k].input),
                        .output = premade_samples[k].output,
                    };
                } else if (k < 100) {
                    var random_instance: std.Random.DefaultPrng = .init(@intCast(k));
                    const random = random_instance.random();
                    var num_true = 1 + random.uintLessThan(usize, 10);
                    var num_false = 1 + random.uintLessThan(usize, 10);
                    // long samples
                    if (k > 90) num_false += 50;
                    if (k > 90) num_true += 50;
                    if (num_true == num_false) {
                        if (random.boolean()) {
                            num_true += 1;
                        } else {
                            num_false += 1;
                        }
                    }
                    const all_elements = try arena.alloc(*const Sexpr, num_true + num_false);
                    @memset(all_elements, f);
                    for (0..num_true) |_| {
                        var index = random.uintLessThan(usize, all_elements.len);
                        while (all_elements[index] == t) {
                            index = random.uintLessThan(usize, all_elements.len);
                        }
                        all_elements[index] = t;
                    }
                    return .{
                        .input = try toList(pool, all_elements),
                        .output = Sexpr.fromBool(num_true > num_false),
                    };
                } else return null;
            }
        }.generate_sample,
    },
    .{
        .fnk_name = &Sexpr.doLit("brainfuck"),
        .generate_sample = struct {
            fn generate_sample(k: usize, pool: *SexprPool, _: std.mem.Allocator) core.OoM!?Sample {
                // TODO: infinite samples
                const prev = Vals.BF.prev;
                const next = Vals.BF.next;
                const inc = Vals.BF.inc;
                const dec = Vals.BF.dec;
                const in = Vals.BF.in;
                const out = Vals.BF.out;
                const loop = Vals.BF.loop;
                const end = Vals.BF.end;
                const premade_samples: []const struct {
                    code: []const *const Sexpr,
                    stdin: []const usize,
                    output: []const usize,
                } = &.{ .{
                    .code = &.{ in, in, out, in, out },
                    .stdin = &.{ 1, 2, 3, 4 },
                    .output = &.{ 2, 3 },
                }, .{
                    .code = &.{
                        inc, inc, out,
                        inc, inc, inc,
                        out, dec, dec,
                        dec, dec, out,
                    },
                    .stdin = &.{},
                    .output = &.{ 2, 5, 1 },
                }, .{
                    .code = &.{ inc, next, next, prev, prev, out },
                    .stdin = &.{},
                    .output = &.{1},
                }, .{
                    .code = &.{
                        inc,  inc,  inc,
                        loop, dec,  next,
                        inc,  inc,  prev,
                        end,  next, out,
                    },
                    .stdin = &.{},
                    .output = &.{6},
                } };
                if (k < premade_samples.len) {
                    const sample = premade_samples[k];
                    return .{
                        .input = try store(pool, Sexpr.doPair(
                            try toList(pool, sample.code),
                            try toListOfPeano(pool, sample.stdin),
                        )),
                        .output = try toListOfPeano(pool, sample.output),
                    };
                } else return null;
            }
        }.generate_sample,
    },
    .{
        // TODO
        .fnk_name = &Sexpr.doLit("interpreter"),
        .generate_sample = struct {
            fn generate_sample(k: usize, pool: *SexprPool, arena: std.mem.Allocator) core.OoM!?Sample {
                if (k != 0) return null;
                var mem = core.VeryPermamentGameStuff.init(arena);
                defer mem.deinit();
                var scoring = try core.ScoringRun.init(
                    \\ bubbleUp {
                    \\      (A . @rest) -> (A . @rest);
                    \\      (@a . @b) -> bubbleUp: @b {
                    \\          (A . @rest) -> (A . (@a . @rest));
                    \\      }
                    \\ }
                , &mem);
                defer scoring.deinit(true);
                const input = try toList(pool, &.{ Vals.uppercase[1], Vals.uppercase[2], Vals.uppercase[0], Vals.uppercase[3] });
                const output = try toList(pool, &.{ Vals.uppercase[0], Vals.uppercase[1], Vals.uppercase[2], Vals.uppercase[3] });
                const fnk_name = try store(pool, Sexpr.doLit("bubbleUp"));
                const fnk_def = scoring.all_fnks.get(fnk_name).?;
                const fnk_def_sexpr = try store(pool, Sexpr.doPair(
                    fnk_name,
                    try core.sexprFromCases(fnk_def.cases.items, pool),
                ));
                return .{
                    .input = try store(pool, Sexpr.doPair(
                        input,
                        try store(pool, Sexpr.doPair(
                            fnk_name,
                            try toList(pool, &.{fnk_def_sexpr}),
                        )),
                    )),
                    .output = output,
                };
            }
        }.generate_sample,
    },
};

fn store(pool: *SexprPool, s: Sexpr) !*const Sexpr {
    const res = try pool.create();
    res.* = s;
    return res;
}

fn toList(pool: *SexprPool, items: []const *const Sexpr) !*const Sexpr {
    return toListWithSentinel(pool, items, Sexpr.builtin.nil);
}

fn toListWithSentinel(pool: *SexprPool, items: []const *const Sexpr, sentinel: *const Sexpr) !*const Sexpr {
    var result = sentinel;
    for (0..items.len) |k| {
        result = try store(pool, Sexpr.doPair(items[items.len - 1 - k], result));
    }
    return result;
}

fn toPeano(pool: *SexprPool, n: usize) !*const Sexpr {
    var result = Sexpr.builtin.nil;
    for (0..n) |_| {
        result = try store(pool, Sexpr.doPair(Vals.peano_succ, result));
    }
    return result;
}

fn toListOfPeano(pool: *SexprPool, ns: []const usize) !*const Sexpr {
    var result = Sexpr.builtin.nil;
    for (0..ns.len) |k| {
        const n = ns[ns.len - k - 1];
        result = try store(pool, Sexpr.doPair(try toPeano(pool, n), result));
    }
    return result;
}

fn randomList(pool: *SexprPool, options: []const *const Sexpr, random: std.Random, len: usize) !*const Sexpr {
    if (len == 0) {
        return Sexpr.builtin.nil;
    } else {
        const first = randomChoice(options, random);
        const rest = try randomList(pool, options, random, len - 1);
        return try store(pool, Sexpr.doPair(first, rest));
    }
}

fn randomChoice(options: []const *const Sexpr, random: std.Random) *const Sexpr {
    assert(options.len > 0);
    return options[random.uintLessThan(usize, options.len)];
}

fn randomSexpr(pool: *SexprPool, atoms: []const *const Sexpr, random: std.Random, max_depth: usize) !*const Sexpr {
    if (max_depth == 0 or random.float(f32) < 0.3) {
        return randomChoice(atoms, random);
    } else {
        return try store(pool, Sexpr.doPair(
            try randomSexpr(pool, atoms, random, max_depth - 1),
            try randomSexpr(pool, atoms, random, max_depth - 1),
        ));
    }
}

const Vals = struct {
    const peano_succ: *const Sexpr = &Sexpr.doLit("N");

    const lowercase: [6]*const Sexpr = .{
        &Sexpr.doLit("a"),
        &Sexpr.doLit("b"),
        &Sexpr.doLit("c"),
        &Sexpr.doLit("d"),
        &Sexpr.doLit("e"),
        &Sexpr.doLit("f"),
    };
    const uppercase: [6]*const Sexpr = .{
        &Sexpr.doLit("A"),
        &Sexpr.doLit("B"),
        &Sexpr.doLit("C"),
        &Sexpr.doLit("D"),
        &Sexpr.doLit("E"),
        &Sexpr.doLit("F"),
    };

    const BF: struct {
        prev: *const Sexpr = &Sexpr.doLit("prev"),
        next: *const Sexpr = &Sexpr.doLit("next"),
        inc: *const Sexpr = &Sexpr.doLit("inc"),
        dec: *const Sexpr = &Sexpr.doLit("dec"),
        in: *const Sexpr = &Sexpr.doLit("in"),
        out: *const Sexpr = &Sexpr.doLit("out"),
        loop: *const Sexpr = &Sexpr.doLit("loop"),
        end: *const Sexpr = &Sexpr.doLit("end"),
    } = .{};
};
